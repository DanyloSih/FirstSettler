#pragma kernel GenerateData
#include "../Noise/ClassicNoise2D.hlsl"
#include "../Structs/VoxelData.compute"
#include "../Structs/HeightAndMaterialHashAssociation.compute"
#include "../BufferHandlers/ChunkSizePrisms.compute"

StructuredBuffer<int3> LocalChunksPositions;
RWStructuredBuffer<VoxelData> ChunkData;
int Octaves;
float Persistence;
float Frequency;
float Amplitude;
float MaxHeight;
float MinHeight;

float GetHeight(float2 noisePosition)
{
    return MinHeight + ClassicNoise(noisePosition / Frequency) * MaxHeight;
}

float GetVolume(float3 globalVoxelPos)
{
    float mainHeight = GetHeight(globalVoxelPos.xz);
    return clamp((mainHeight - globalVoxelPos.y) * 0.5, 0, 1);
}

[numthreads(1, 4, 1)]
void GenerateData (uint3 id : SV_DispatchThreadID)
{    
    int chunkId = id.x;
    int voxelIndex = id.y;
    
    RectPrism chunkVoxelsPrism = GetChunkVoxelsPrism();
    if (voxelIndex >= chunkVoxelsPrism.Volume)
    { 
        return;
    }
    
    RectPrism chunkCubesPrism = GetChunkCubesPrism();
    
    int3 localChunkPos = LocalChunksPositions[chunkId];
    int3 localVoxelPos = IndexToPoint(chunkVoxelsPrism, voxelIndex);
    int3 globalChunkPos = localChunkPos * chunkCubesPrism.Size;
    int3 globalVoxelPos = globalChunkPos + localVoxelPos;
    int i = chunkId * chunkVoxelsPrism.Volume + voxelIndex;
     
    VoxelData voxel;
    voxel.Volume = GetVolume(globalVoxelPos);
    voxel.MaterialHash = GetMaterialKeyHashByHeight(globalVoxelPos.y);
    
    ChunkData[i] = voxel;
}
